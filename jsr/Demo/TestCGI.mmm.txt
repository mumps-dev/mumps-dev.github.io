TestCGI ; CGI-Call for GTM
 ; Based on original code by Winfried Bantel - Stuttgart - Germany
 ;
 New i,data,entry
 Write "CONTENT-TYPE: TEXT/HTML",!!
 Set $ZTRAP="Do Error^"_$Text(+0)_"($ZSTATUS)"
 ;
 If $ZTRNLNM("REQUEST_METHOD")="POST" Do
 . ; Retrieve variables from <stdin>
 . Read data#$ZTRNLNM("CONTENT_LENGTH"):5
 . QUit
 Else  Do
 . ; Retrieve variables from Query-String
 . Set data=$ZTRNLNM("QUERY_STRING")
 . Quit
 ;
 For i=1:1:$Length(data,"&") Do
 . New ind,pc,val
 . Set pc=$Piece(data,"&",i)
 . Set ind=$$URLin($P(pc,"=",1)),val=$$URLin($P(pc,"=",2))
 . Set:ind'="" %KEY(ind)=val
 . Quit
 ;
 Set entry=$Get(%KEY("Action"))
 If $Get(entry)="" Do Error("No action requested.")
 ; Error trap does not return
 ;
 i entry="get" d  h
 . zsystem "setenv >get.lst"
 . w "<html><title>Ed's GT.M system</title><body><h1>GET</h1>"
 . w !,"<form action=""TestCGI"" method=""POST"">"
 . w !,"<input type=hidden name=""Action"" value=""post"">"
 . w !,"<input type=text name=""text"" value=""textbox"">"
 . w !,"<input type=submit name=""btn"" value=""PostIt"">"
 . w !,"</form></body></html>",!
 . q
 i entry="post" d  h
 . zsystem "setenv >post.lst"
 . w "<html><title>Ed's GT.M system</title><body><h1>POST!</h1></body></html>"
 . q
 i entry="abc" d  h
 . w "<html><title>Ed's GT.M system</title><body><h1>ABC OK!</h1></body></html>"
 . q
 ;
 D Error("Invalid action requested: """_entry_""".")
 ; Error trap does not return
 ;
HTMLout(x) New e,i,r
 Set r="" For i=1:1:$Length(x) Do
 . Set e=$Extract(x,i)
 . If e="&" Set r=r_"&amp;" Quit
 . If e="<" Set r=r_"&lt;" Quit
 . If e=">" Set r=r_"&gt;" Quit
 . If $Ascii(e)>126 Set r=r_"&#"_$Ascii(e)_";" Quit
 . Set r=r_e
 . Quit
 Quit r
 ;
URLout(x) New e,i,hex,r
 Set hex="0123456789abcdef"
 Set r="" For i=1:1:$Length(x) Do
 . Set e=$Extract(x,i)
 . If e?1AN Set r=r_e Quit
 . If e=" " Set r=r_"+" Quit
 . Set e=$ASCII(e),r=r_"%"_$Extract(hex,e\16+1)_$Extract(hex,e#16+1)
 . Quit
 Quit r
 ;
URLin(x) New c,e,hex,i,p,r,z
 Set hex="0123456789abcdef",z=$tr(x,"ABCDEF","abcdef")
 Set r="" For i=1:1:$Length(x) Do
 . Set e=$Extract(x,i)
 . If e="+" Set r=r_" " Quit
 . If e="%" Do  Quit
 . . Set c=$Find(hex,$Extract(z,i+1))-2*16+$Find(hex,$Extract(z,i+2))-2
 . . Set r=r_$Char(c),i=i+2
 . . Quit
 . Set r=r_e
 . Quit
 Quit r
 ;
Error(MSG) ;
 Write "<html><title>JSR Error Report</title><body>"
 Write !,"<h2><font color=#ff0000>Error: "_$$HTMLout(MSG)_"</h2>"
 Write !,"<h3>GT.M version is ",$ZVERSION,"</h3>"
 Write !,"<pre>"
 ZShow "*"
 Write !,"</pre>"
 Write !,"</body></html>"
 Halt
 ;
